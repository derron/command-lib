// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.4'
    }
}

allprojects {
    repositories {
        jcenter()
    }
    group = 'la.dahuo'
    version = '1.0.0'
}

subprojects {
    apply plugin: 'maven'
    apply plugin: 'java'
    apply plugin: 'com.jfrog.bintray'

    sourceCompatibility = JavaVersion.VERSION_1_7
    targetCompatibility = JavaVersion.VERSION_1_7

    // custom tasks for creating source/javadoc jars
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar, javadocJar
    }

    bintray {
        user = System.getenv('BINTRAY_USER');
        key = System.getenv('BINTRAY_KEY');

        configurations = ['archives'] //When uploading configuration files

        dryRun = false //Whether to run this as dry-run, without deploying
        publish = true //If version should be auto published after an upload
        pkg {
            repo = 'maven'
            name = project.group + ":" + project.name
            websiteUrl = 'https://github.com/derron/command-lib'
            issueTrackerUrl = 'https://github.com/derron/command-lib/issues'
            vcsUrl = 'https://github.com/derron/command-lib.git'
            licenses = ['Apache-2.0']
            labels = ['android', 'command']
            publicDownloadNumbers = true
        }
    }
}